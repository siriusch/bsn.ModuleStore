! -----------------------------------------------------------------------------------
! T-SQL Script Analyzer
! -----------------------------------------------------------------------------------

"Name"         = 'T-SQL CREATE Statements'
"Version"      = '2005'
"About"        = 'Parser for T-SQL subset to be supported by ModuleStore'
"Author"       = 'Ars?ne von Wyss'

"Character Mapping" = 'Unicode'

"Start Symbol" = <StatementList>

! CLR UDTs are missing, but may be implemented later

! =============================================================================
! Comments
! =============================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '--'

! =============================================================================
! Terminals
! =============================================================================

{String Ch}        = {Printable} - ['']
{Id Ch Standard}   = {Alphanumeric} + [_]
{Id Ch Extended}   = {Printable} - ['['#']']
{Hex Ch}           = [0123456789ABCDEF]
{Nonspace Ch}      = {Printable} - {Whitespace}

StringLiteral   = N?(''{String Ch}*'')+
IntegerLiteral  = {Digit}+
RealLiteral     = {Digit}+'.'{Digit}+
HexLiteral      = 0x{Hex Ch}+

! We use a simplified name convention, which does not allow user/schema, database or server to be specified
Id             = {Letter}{Id Ch Standard}* | '['{Id Ch Extended}+']' | '"'{Id Ch Standard}*'"'
LocalId        = '@'{Letter}{Id Ch Standard}*
SystemVarId    = '@@'{Letter}{Id Ch Standard}*
SystemFuncId   = '::'{Letter}{Id Ch Standard}*
TempTableId    = '#'{Letter}{Id Ch Standard}* | '[#'{Id Ch Extended}+']' | '"#'{Id Ch Standard}*'"'

! The reserved keywords as per MSDN: http://msdn.microsoft.com/en-us/library/ms189822.aspx
! By specifying them explicitly here, we should not see those in the log as implicit when generating the grammar tables, and the unsed ones are still reserved keywords
ADD = ADD
EXISTS = EXISTS
PRECISION = PRECISION
ALL = ALL
EXIT = EXIT
PRIMARY = PRIMARY
ALTER = ALTER
EXTERNAL = EXTERNAL
PRINT = PRINT
AND = AND
FETCH = FETCH
! PROC = PROC -- is covered by PROCEDURE
ANY = ANY
FILE = FILE
PROCEDURE = PROC(EDURE)?
AS = AS
FILLFACTOR = FILLFACTOR
PUBLIC = PUBLIC
ASC = ASC
FOR = FOR
RAISERROR = RAISERROR
AUTHORIZATION = AUTHORIZATION
FOREIGN = FOREIGN
READ = READ
BACKUP = BACKUP
FREETEXT = FREETEXT
READTEXT = READTEXT
BEGIN = BEGIN
FREETEXTTABLE = FREETEXTTABLE
RECONFIGURE = RECONFIGURE
BETWEEN = BETWEEN
FROM = FROM
REFERENCES = REFERENCES
BREAK = BREAK
FULL = FULL
REPLICATION = REPLICATION
BROWSE = BROWSE
FUNCTION = FUNCTION
RESTORE = RESTORE
BULK = BULK
GOTO = GOTO
RESTRICT = RESTRICT
BY = BY
GRANT = GRANT
RETURN = RETURN
CASCADE = CASCADE
GROUP = GROUP
REVERT = REVERT
CASE = CASE
HAVING = HAVING
REVOKE = REVOKE
CHECK = CHECK
HOLDLOCK = HOLDLOCK
RIGHT = RIGHT
CHECKPOINT = CHECKPOINT
IDENTITY = IDENTITY
ROLLBACK = ROLLBACK
CLOSE = CLOSE
IDENTITY_INSERT = IDENTITY_INSERT
ROWCOUNT = ROWCOUNT
CLUSTERED = CLUSTERED
IDENTITYCOL = IDENTITYCOL
ROWGUIDCOL = ROWGUIDCOL
COALESCE = COALESCE
IF = IF
RULE = RULE
COLLATE = COLLATE
IN = IN
SAVE = SAVE
COLUMN = COLUMN
INDEX = INDEX
SCHEMA = SCHEMA
COMMIT = COMMIT
INNER = INNER
SECURITYAUDIT = SECURITYAUDIT
COMPUTE = COMPUTE
INSERT = INSERT
SELECT = SELECT
CONSTRAINT = CONSTRAINT
INTERSECT = INTERSECT
SESSION_USER = SESSION_USER
CONTAINS = CONTAINS
INTO = INTO
SET = SET
CONTAINSTABLE = CONTAINSTABLE
IS = IS
SETUSER = SETUSER
CONTINUE = CONTINUE
JOIN = JOIN
SHUTDOWN = SHUTDOWN
CONVERT = CONVERT
KEY = KEY
SOME = SOME
CREATE = CREATE
KILL = KILL
STATISTICS = STATISTICS
CROSS = CROSS
LEFT = LEFT
SYSTEM_USER = SYSTEM_USER
CURRENT = CURRENT
LIKE = LIKE
TABLE = TABLE
CURRENT_DATE = CURRENT_DATE
LINENO = LINENO
TABLESAMPLE = TABLESAMPLE
CURRENT_TIME = CURRENT_TIME
LOAD = LOAD
TEXTSIZE = TEXTSIZE
CURRENT_TIMESTAMP = CURRENT_TIMESTAMP
MERGE = MERGE
THEN = THEN
CURRENT_USER = CURRENT_USER
NATIONAL = NATIONAL
TO = TO
CURSOR = CURSOR
NOCHECK = NOCHECK
TOP = TOP
DATABASE = DATABASE
NONCLUSTERED = NONCLUSTERED
TRAN = TRAN
DBCC = DBCC
NOT = NOT
TRANSACTION = TRANSACTION
DEALLOCATE = DEALLOCATE
NULL = NULL
TRIGGER = TRIGGER
DECLARE = DECLARE
NULLIF = NULLIF
TRUNCATE = TRUNCATE
DEFAULT = DEFAULT
OF = OF
TSEQUAL = TSEQUAL
DELETE = DELETE
OFF = OFF
UNION = UNION
DENY = DENY
OFFSETS = OFFSETS
UNIQUE = UNIQUE
DESC = DESC
ON = ON
UNPIVOT = UNPIVOT
DISK = DISK
OPEN = OPEN
UPDATE = UPDATE
DISTINCT = DISTINCT
OPENDATASOURCE = OPENDATASOURCE
UPDATETEXT = UPDATETEXT
DISTRIBUTED = DISTRIBUTED
OPENQUERY = OPENQUERY
USE = USE
DOUBLE = DOUBLE
OPENROWSET = OPENROWSET
USER = USER
DROP = DROP
OPENXML = OPENXML
VALUES = VALUES
DUMP = DUMP
OPTION = OPTION
VARYING = VARYING
ELSE = ELSE
OR = OR
VIEW = VIEW
END = END
ORDER = ORDER
WAITFOR = WAITFOR
ERRLVL = ERRLVL
OUTER = OUTER
WHEN = WHEN
ESCAPE = ESCAPE
OVER = OVER
WHERE = WHERE
EXCEPT = EXCEPT
PERCENT = PERCENT
WHILE = WHILE
! EXEC = EXEC -- is covered by EXECUTE
PIVOT = PIVOT
WITH = WITH
EXECUTE = EXEC(UTE)?
PLAN = PLAN
WRITETEXT = WRITETEXT

! Compound reserved words to keep the grammar context-free
RETURNS_NULL_ON_NULL_INPUT = RETURNS{Whitespace}+NULL{Whitespace}+ON{Whitespace}+NULL{Whitespace}+INPUT
CALLED_ON_NULL_INPUT = CALLED{Whitespace}+ON{Whitespace}+NULL{Whitespace}+INPUT
FOR_XML = FOR{Whitespace}+XML
FOR_PATH = FOR{Whitespace}+PATH
FOR_UPDATE = FOR{Whitespace}+UPDATE
FOR_VALUE = FOR{Whitespace}+VALUE
FOR_PROPERTY = FOR{Whitespace}+PROPERTY
XML_INDEX = XML{Whitespace}+INDEX
XML_SCHEMA_COLLECTION = XML{Whitespace}+SCHEMA{Whitespace}+COLLECTION
USING_XML_INDEX = USING{Whitespace}+XML{Whitespace}+INDEX
WITH_CHANGE_TRACKING = WITH{Whitespace}+CHANGE_TRACKING
WITH_RECOMPILE = WITH{Whitespace}+RECOMPILE
WITH_VIEW_METADATA = WITH{Whitespace}+VIEW_METADATA
WITH_CHECK_OPTION = WITH{Whitespace}+CHECK{Whitespace}+OPTION
ENABLE_TRIGGER = ENABLE{Whitespace}+TRIGGER
DISABLE_TRIGGER = DISABLE{Whitespace}+TRIGGER
ADD_PERSISTED = ADD{Whitespace}+PERSISTED
DROP_PERSISTED = DROP{Whitespace}+PERSISTED
BEGIN_TRY = BEGIN{Whitespace}+TRY
END_TRY = END{Whitespace}+TRY
BEGIN_CATCH = BEGIN{Whitespace}+CATCH
END_CATCH = END{Whitespace}+CATCH
FULLTEXT_INDEX = FULLTEXT{Whitespace}+INDEX
WITH_FILLFACTOR = WITH{Whitespace}+FILLFACTOR
READ_ONLY = READ{Whitespace}+ONLY
NO_POPULATION = NO{Whitespace}+POPULATION
NO_ACTION = NO{Whitespace}+ACTION
TYPE_COLUMN = TYPE{Whitespace}+COLUMN
PARTITION_BY = PARTITION{Whitespace}+BY
INSTEAD_OF = INSTEAD{Whitespace}+OF
INCLUDE_ = INCLUDE{Whitespace}*'('
COUNT_ = COUNT{Whitespace}*'('
CAST_ = CAST{Whitespace}*'('
_RETURNS = ')'{Whitespace}*RETURNS
LANGUAGE_LCID = LANGUAGE{Whitespace}+(0x{Hex Ch}+|{Digit}+|''{String Ch}+('-'{String Ch}+)?'')
OUTPUT = OUT(PUT)?

! =============================================================================
! Common Terminals (Names, Literals, ...)
! =============================================================================

<AliasName> ::= Id

<CollationName> ::= Id

<ColumnName> ::= Id

<ColumnNameQualified> ::= <ColumnName>
                        | <TableName> '.' <ColumnName>
                        | <VariableName> '.' <ColumnName>

<ColumnNameList> ::= <ColumnNameList> ',' <ColumnName>
                   | <ColumnName>

<ColumnWild> ::= '*'

<ColumnWildQualified> ::= <ColumnWild>
                        | <TableName> '.' <ColumnWild>
                        | <VariableName> '.' <ColumnWild>

<ColumnWildNameQualified> ::= <ColumnNameQualified>
                            | <ColumnWildQualified>

<ConstraintName> ::= Id

<CursorName> ::= Id

<FunctionName> ::= Id
                 | SystemFuncId

<IndexName> ::= Id

<LabelName> ::= Id

<ParameterName> ::= LocalId

<ProcedureName> ::= Id

<ProcedureNameQualified> ::= <ProcedureName>
                           | <SchemaName> '.' <ProcedureName>

<TableName> ::= Id
              | TempTableId

<TableNameQualified> ::= <TableName>
                       | <SchemaName> '.' <TableName>

<TriggerName> ::= Id

<TypeName> ::= Id
             | Id '(' Id ')' ! e.g. VARCHAR(MAX)
             | Id '(' <IntegerLiteral> ')' ! e.g. VARCHAR(20)
             | Id '(' <IntegerLiteral> ',' <IntegerLiteral> ')' ! e.g. DECIMAL(10,5)

<TypeNameQualified> ::= <TypeName>
                      | <SchemaName> '.' <TypeName>

<SchemaName> ::= Id

<SystemVariableName> ::= SystemVarId

<VariableName> ::= LocalId

<ViewName> ::= Id

<XmlElementName> ::= Id

<XmlSchemaCollectionName> ::= Id

<OptionalAs> ::= AS
               |

<NullLiteral> ::= NULL

<NullCheck> ::= <NullLiteral>
              | NOT <NullLiteral>

<StringLiteral> ::= StringLiteral

<OptionalCollate> ::= COLLATE <CollationName>
                    |

<CollableStringLiteral> ::= <StringLiteral> <OptionalCollate>

<IntegerLiteral> ::= IntegerLiteral
                   | HexLiteral

<NumberLiteral> ::= <IntegerLiteral>
                  | RealLiteral

<Literal> ::= <NumberLiteral>
            | <CollableStringLiteral>
            | <NullLiteral>

<Toggle> ::= ON
           | OFF

! =============================================================================
! Any statements allowed as SQL statements in code
! =============================================================================

! In T-SQL, the terminator (;) is usually not required. However, this introduces some ambiguities, so that we require it. 

<StatementGroup> ::= <StatementBlock>
                   | <Statement> 

<StatementBlock> ::= BEGIN <StatementList> END

<StatementList> ::= <StatementGroup>
                  | <StatementGroup> <Terminator>
                  | <StatementGroup> <Terminator> <StatementList>

<Terminator> ::= <Terminator> ';'
               | ';'

<Statement> ::= <DDLStatement>
              | <DMLStatement>
              | <PrgStatement>

<DDLStatement> ::= <CreateFulltextStatement>
                 | <DropFulltextStatement>
                 | <CreateFunctionStatement>
                 | <DropFunctionStatement>
                 | <CreateIndexStatement>
                 | <DropIndexStatement>
                 | <CreateProcedureStatement>
                 | <DropProcedureStatement>
                 | <CreateTableStatement>
                 | <AlterTableStatement> ! We don't need anything but this to handle data update scripts
                 | <DropTableStatement>
                 | <CreateTriggerStatement>
                 | <DropTriggerStatement>
                 | <CreateViewStatement>
                 | <DropViewStatement>
                 | <CreateXmlSchemaCollectionStatement>
                 | <DropXmlSchemaCollectionStatement>

<DMLStatement> ::= <SelectStatement>
                 | <InsertStatement>
                 | <UpdateStatement>
                 | <DeleteStatement>

<PrgStatement> ::= <AnyStatement>
                 | <BreakStatement>
                 | <CloseStatement>
                 | <ContinueStatement>
                 | <DeallocateStatement>
                 | <DeclareStatement>
                 | <ExecuteStatement>
                 | <GotoStatement>
                 | <IfStatement>
                 | <OpenStatement>
                 | <PrintStatement>
                 | <ReturnStatement>
                 | <SetStatement>
                 | <TryCatchStatement>
                 | <WaitforStatement>
                 | <WhileStatement>

! =============================================================================
! Control-of-Flow and simple statements
! =============================================================================

<BreakStatement> ::= BREAK

<ContinueStatement> ::= CONTINUE

<GotoStatement> ::= GOTO <LabelName>

<IfStatement> ::= IF <Predicate> THEN <IfElseStatement>
                | IF <Predicate> <Statement>

! To avoid the hanging ELSE problem/ambiguity, we just require a block - even if this is more restrictive than T-SQL
<IfElseStatement> ::= <StatementBlock> ELSE <StatementGroup>
                    | <StatementGroup>

<ReturnStatement> ::= RETURN
                    | RETURN <Expression>

<TryCatchStatement> ::= BEGIN_TRY <StatementList> END_TRY BEGIN_CATCH <StatementList> END_CATCH

<WhileStatement> ::= WHILE <Expression> <StatementGroup>

<WaitforStatement> ::= WAITFOR Id <StringValue>

<PrintStatement> ::= PRINT <Expression>

<AnyStatement> ::= Id <ExpressionList>

! =============================================================================
! DECLARE and Cursor statements
! =============================================================================

<DeclareStatement> ::= DECLARE <VariableName> <OptionalAs> TABLE <TableTypeDefinitionGroup>
                     | DECLARE <DeclareItemList>
                     | DECLARE <CursorName> CURSOR <CursorOptionList> FOR <SelectStatement>
                     | DECLARE <CursorName> CURSOR <CursorOptionList> FOR <SelectStatement> <CursorUpdate>

<DeclareItem> ::= <VariableName> CURSOR
                | <VariableName> <OptionalAs> <TypeNameQualified>
                | <VariableName> <OptionalAs> <TypeNameQualified> '=' <Expression>

<DeclareItemList> ::= <DeclareItemList> ',' <DeclareItem>
                    | <DeclareItem>

<CursorOptionList> ::= Id <CursorOptionList>
                     |

<CloseStatement> ::= CLOSE <GlobalOrLocalCursor>

<OpenStatement> ::= OPEN <GlobalOrLocalCursor>

<DeallocateStatement> ::= DEALLOCATE <GlobalOrLocalCursor>

<GlobalOrLocalCursor> ::= <VariableName>
                        | <CursorName>
                        | Id <CursorName> ! Id should be GLOBAL

<CursorUpdate> ::= FOR_UPDATE OF <ColumnNameList>
                 | FOR_UPDATE
                 | READ_ONLY

! =============================================================================
! Common helper constructs
! =============================================================================

<OptionalDefault> ::= '=' <Literal>
                    |

<ReadableRowset> ::= <VariableName>
                   | <TableNameQualified>
                   | '(' <SelectGroup> ')'
                   | <Openxml>
                   | <FunctionCall>

<WriteableRowset> ::= <VariableName>
                    | <TableName>
!                    | <Openrowset> -- not supported
!                    | <Openquery> -- not supported

<Openxml> ::= OPENXML '(' <VariableName> ',' <StringValue> <OptionalOpenxmlFlags> ')' <OptionalOpenxmlSchema>

<OptionalOpenxmlFlags> ::= ',' <IntegerLiteral>
                         |

<OptionalOpenxmlSchema> ::= WITH '(' <OpenxmlSchemaGroup> ')'
                          |

<OpenxmlSchemaGroup> ::= <TableNameQualified>
                       | <OpenxmlColumnList>

<OpenxmlColumnList> ::= <OpenxmlColumn> ',' <OpenxmlColumnList>
                      | <OpenxmlColumn>

<OpenxmlColumn> ::= <ColumnName> <TypeName> <OptionalOpenxmlColumnPattern>

<OptionalOpenxmlColumnPattern> ::= <StringLiteral>
                                 |

! =============================================================================
! SET (both variables and options, but not yet CLR UDT properties and fields)
! =============================================================================

<SetStatement> ::= <SetVariableStatement>
                 | <SetOptionStatement>

<SetVariableStatement> ::= SET <VariableName> '=' <Expression>

<SetOptionStatement> ::= SET Id <SetValueList>
                       | SET IDENTITY_INSERT <TableName> <Toggle>

<SetValueList> ::= <SetValue> <SetValueList>
                 | <SetValue>

<SetValue> ::= Id
             | <Toggle>
             | <IntegerLiteral>
             | <StringValue>

<StringValue> ::= <StringLiteral>
                | <VariableName>

! =============================================================================
! CREATE/DROP FULLTEXT
! =============================================================================

! We don't allow to specify the catalog name, since the catalog is automatically assigned (module name)

<CreateFulltextStatement> ::= CREATE FULLTEXT_INDEX ON TABLE <TableName> <FulltextColumnGroup> KEY INDEX <IndexName> <FulltextChangeTracking>

<FulltextColumnGroup> ::= '(' <FulltextColumnList> ')'
                        |

<FulltextColumnList> ::= <FulltextColumn> ',' <FulltextColumnList>
                       | <FulltextColumn>

<FulltextColumn> ::= <ColumnName> <FulltextColumnType> <FulltextColumnLanguage>

<FulltextColumnType> ::= TYPE_COLUMN <TypeNameQualified>
                       |

<FulltextColumnLanguage> ::= LANGUAGE_LCID
                           |

<FulltextChangeTracking> ::= WITH_CHANGE_TRACKING <FulltextChangeTrackingName>
                           |

<FulltextChangeTrackingName> ::= Id
                               | OFF
                               | OFF ',' NO_POPULATION

<DropFulltextStatement> ::= DROP FULLTEXT_INDEX ON <TableName>

! =============================================================================
! CREATE/DROP FUNCTION
! =============================================================================

! We don't allow to specify the schema name, it is implicit (module name)

<CreateFunctionStatement> ::= CREATE FUNCTION <FunctionName> <FunctionParameterGroup> <FunctionType>

<FunctionParameterGroup> ::= '(' <FunctionParameterList> _RETURNS
                           | '(' _RETURNS

<FunctionParameterList> ::= <FunctionParameter> ',' <FunctionParameterList>
                          | <FunctionParameter>

<FunctionParameter> ::= <ParameterName> <OptionalAs> <TypeNameQualified> <OptionalDefault>

<FunctionType> ::= <FunctionScalar>
                 | <FunctionInline>
                 | <FunctionTable>

<FunctionScalar> ::= <TypeName> <FunctionOptionGroup> <OptionalAs> <StatementBlock>

<FunctionInline> ::= TABLE <FunctionOptionGroup> <OptionalAs> RETURN <FunctionInlineSelect>

<FunctionInlineSelect> ::= <SelectStatement>
                         | '(' <SelectStatement> ')'

<FunctionTable> ::= <VariableName> TABLE <TableTypeDefinitionGroup> <FunctionOptionGroup> <OptionalAs> <StatementBlock>

<FunctionOptionGroup> ::= WITH <FunctionOption>
                        |

! The options ENCRYPTION and SCHEMABINDING are handled by the module and therefore not allowed
<FunctionOption> ::= RETURNS_NULL_ON_NULL_INPUT
                   | CALLED_ON_NULL_INPUT

<DropFunctionStatement> ::= DROP FUNCTION <FunctionName>

! =============================================================================
! CREATE/DROP PROCEDURE and EXECUTE
! =============================================================================

<CreateProcedureStatement> ::= CREATE PROCEDURE <ProcedureName> <ProcedureParameterGroup> <ProcedureOptionGroup> <ProcedureFor> AS <StatementBlock>

<ProcedureParameterGroup> ::= <ProcedureParameterList>
                            |

<ProcedureParameterList> ::= <ProcedureParameter> ',' <ProcedureParameterList>
                           | <ProcedureParameter>

<ProcedureParameter> ::= <ParameterName> <TypeNameQualified> <OptionalVarying> <OptionalDefault> <OptionalOutput>

<OptionalVarying> ::= VARYING
                    |

<OptionalOutput> ::= OUTPUT
                   |

<ProcedureOptionGroup> ::= WITH_RECOMPILE
                         |

<ProcedureFor> ::= FOR REPLICATION
                 |

<DropProcedureStatement> ::= DROP PROCEDURE <ProcedureName>

<ExecuteStatement> ::= EXECUTE <OptionalVariableAssignment> <ProcedureNameQualified> <ExecuteParameterGroup> <ProcedureOptionGroup>

<ExecuteParameterGroup> ::= <ExecuteParameterList>
                          |

<ExecuteParameterList> ::= <ExecuteParameter> ',' <ExecuteParameterList>
                         | <ExecuteParameter>

<ExecuteParameter> ::= <TableNameQualified> ! for the system "sp_*" SPs
                     | <ParameterName> '=' <ExecuteParameterValue> <OptionalOutput>
                     | <ExecuteParameterValue> <OptionalOutput>

<ExecuteParameterValue> ::= <VariableName>
                          | <SystemVariableName>
                          | <Literal>

<OptionalVariableAssignment> ::= <VariableName> '='
                               |

! =============================================================================
! CREATE/ALTER/DROP TABLE
! =============================================================================

! We don't support the ON option, filegroup is managed by module manager

<CreateTableStatement> ::= CREATE TABLE <TableName> <TableTypeDefinitionGroup> 

<AlterTableStatement> ::= ALTER TABLE <TableName> <AlterTableKind>

<AlterTableKind> ::= <AlterColumn>
                   | ADD <ColumnOrConstraintList>
                   | <WithCheck> ADD <ColumnOrConstraintList>
                   | <DropColumn>
                   | <DropConstraint>

<AlterColumn> ::= ALTER COLUMN <ColumnName> <AlterColumnKind>

<AlterColumnKind> ::= <ColumnTypeDefinition>
                    | ADD <ConstraintRowguid>
                    | DROP <ConstraintRowguid>
                    | ADD_PERSISTED
                    | DROP_PERSISTED
                    | ADD <NotForReplication>
                    | DROP <NotForReplication>
                    | ADD <TableTypeDefinition>
                    | <WithCheck> ADD <TableTypeDefinition>
                    | <DropConstraint>
                    | <DropColumn>
                    | <WithCheck> <AlterConstraint> 
                    | <AlterConstraint>
                    | <EnableOrDisableTrigger>

<EnableOrDisableTrigger> ::= ENABLE_TRIGGER <AllOrTriggerNameList>
                           | DISABLE_TRIGGER <AllOrTriggerNameList>

<AllOrTriggerNameList> ::= ALL
                        | <TriggerNameList>

<TriggerNameList> ::= <TriggerName> ',' <TriggerNameList>
                    | <TriggerName>

<AlterConstraint> ::= <CheckOrNocheck> CONSTRAINT <AllOrConstraintNameList>

<AllOrConstraintNameList> ::= ALL
                        | <ConstraintNameList>

<ConstraintNameList> ::= <ConstraintName> ',' <ConstraintNameList>
                       | <ConstraintName>

<WithCheck> ::= WITH <CheckOrNocheck>

<CheckOrNocheck> ::= CHECK
                   | NOCHECK

<ColumnOrConstraintList> ::= <ColumnOrConstraint> ',' <ColumnOrConstraintList>
                           | <ColumnOrConstraint>

<ColumnOrConstraint> ::= <ColumnDefinition>
                       | <NamedTableConstraint>

<DropColumn> ::= DROP COLUMN <ColumnName>

<DropConstraint> ::= DROP CONSTRAINT <ConstraintName>
                   | DROP <ConstraintName>

<DropTableStatement> ::= DROP TABLE <TableName>

! =============================================================================
! CREATE/DROP VIEW
! =============================================================================

<CreateViewStatement> ::= CREATE VIEW <ViewName> <ColumnNameGroup> <ViewOptionalAttribute> AS <SelectStatement> <ViewOptionalCheckOption>

<ColumnNameGroup> ::= '(' <ColumnNameList> ')'
                    |

<ViewOptionalAttribute> ::= WITH_VIEW_METADATA
                          |

<ViewOptionalCheckOption> ::= WITH_CHECK_OPTION
                            |

<DropViewStatement> ::= DROP VIEW <ViewName>

! =============================================================================
! CREATE/DROP INDEX
! =============================================================================

! We don't support the ON option, filegroup is managed by module manager

<CreateIndexStatement> ::= CREATE <ConstraintUnique> <ConstraintCluster> INDEX <IndexName> ON <TableName> '(' <IndexColumnList> ')' <IndexInclude> <IndexOptionGroup>
                         | CREATE <IndexPrimary> XML_INDEX <IndexName> ON <TableName> '(' <ColumnName> ')' <IndexUsing> <IndexOptionGroup>

<IndexColumnList> ::= <IndexColumn> ',' <IndexColumnList>
                    | <IndexColumn>

<IndexColumn> ::= <ColumnName> <OrderType> 

<IndexInclude> ::= INCLUDE_ <ColumnNameList> ')'

<IndexPrimary> ::= PRIMARY
                 |

<IndexOptionGroup> ::= WITH '(' <IndexOptionList> ')'
                     |

<IndexOptionList> ::= <IndexOption> ',' <IndexOptionList>
                    | <IndexOption>

<IndexOption> ::= Id '=' <IntegerLiteral>
                | Id '=' <Toggle>

<IndexUsing> ::= USING_XML_INDEX <IndexName> <IndexFor>
               |

<IndexFor> ::= FOR_VALUE
             | FOR_PATH
             | FOR_PROPERTY

<DropIndexStatement> ::= DROP INDEX <IndexName> ON <TableName> <IndexOptionGroup>

! =============================================================================
! CREATE/DROP TRIGGER
! =============================================================================

! Only DML triggers are supported

<CreateTriggerStatement> ::= CREATE TRIGGER <TriggerName> ON <TableName> <TriggerType> <TriggerOperationList> <OptionalNotForReplication> AS <StatementGroup>

<TriggerType> ::= FOR
                | INSTEAD_OF
                | Id ! AFTER is not a keyword

<TriggerOperationList> ::= <TriggerOperation> ',' <TriggerOperationList>
                         | <TriggerOperation>

<TriggerOperation> ::= INSERT
                     | UPDATE
                     | DELETE

<OptionalNotForReplication> ::= <NotForReplication>
                              |

<NotForReplication> ::= NOT FOR REPLICATION

<DropTriggerStatement> ::= DROP TRIGGER <TriggerName>

! =============================================================================
! CREATE/DROP XML SCHEMA COLLECTION
! =============================================================================

<CreateXmlSchemaCollectionStatement> ::= CREATE XML_SCHEMA_COLLECTION <XmlSchemaCollectionName> AS <Expression>

<DropXmlSchemaCollectionStatement> ::= DROP XML_SCHEMA_COLLECTION <XmlSchemaCollectionName>

! =============================================================================
! Table/Column Type Definitions
! =============================================================================

<TableTypeDefinitionGroup> ::= '(' <TableTypeDefinitionList> ')'

<TableTypeDefinitionList> ::= <TableTypeDefinition> ',' <TableTypeDefinitionList>
                            | <TableTypeDefinition>

! This does not match the BNF in the SQL Books Online 2005, but seems to be the correct syntax
<TableTypeDefinition> ::= <ColumnDefinition>
                        | <ColumnComputedDefinition>
                        | <NamedTableConstraint>

<NamedTableConstraint> ::= <NamedConstraint> <TableConstraint>
                         | <TableConstraint>

<TableConstraint> ::= <ConstraintPrimaryKey> <ConstraintCluster> '(' <IndexColumnList> ')' <ConstraintIndex>
                   | <ConstraintForeignKey>
                   | <ConstraintCheck>

<ConstraintForeignKey> ::= FOREIGN KEY <ForeignKeyColumnGroup> REFERENCES <TableName> <OptionalForeignKeyRefColumnGroup> <ForeignKeyActionList>

<ForeignKeyActionList> ::= <OnDeleteAction> <ForeignKeyActionList>
                         | <OnUpdateAction> <ForeignKeyActionList>
                         |

<OnDeleteAction> ::= ON DELETE <Action>

<OnUpdateAction> ::= ON UPDATE <Action>

<Action> ::= NO_ACTION
           | CASCADE
           | SET NULL
           | SET DEFAULT

<OptionalForeignKeyRefColumnGroup> ::= <ForeignKeyColumnGroup>
                                     |

<ForeignKeyColumnGroup> ::= '(' <ForeignKeyColumnList> ')'

<ForeignKeyColumnList> ::= <ColumnName> ',' <ForeignKeyColumnList>
                         | <ColumnName>

<ColumnDefinition> ::= <ColumnName> <ColumnTypeDefinition>

<ColumnTypeDefinition> ::= <TypeNameQualified> <ColumnConstraintList>

<ConstraintDefault> ::= DEFAULT '(' <Expression> ')'

<ConstraintCollate> ::= COLLATE <CollationName>

<ConstraintRowguid> ::= ROWGUIDCOL

<ConstraintIdentity> ::= IDENTITY <IdentityRange>

<IdentityRange> ::= '(' IntegerLiteral ',' IntegerLiteral ')'
                        |

<ColumnConstraintList> ::= <NamedColumnConstraint> <ColumnConstraintList>
                         | 

<NamedConstraint> ::= CONSTRAINT <ConstraintName>

<NamedColumnConstraint> ::= <NamedConstraint> <ColumnConstraint>
                          | <ColumnConstraint>
                          | <ConstraintCollate>
                          | <ConstraintRowguid>
                          | <ConstraintIdentity>
                          | <NullCheck>

<ColumnConstraint> ::= <ConstraintPrimaryKey> <ConstraintCluster> <ConstraintIndex>
                     | <ConstraintCheck>
                     | <ConstraintDefault>
                     | <ConstraintForeignKey>

<ColumnComputedDefinition> ::= <ColumnName> AS <Expression>

<ConstraintPrimaryKey> ::= PRIMARY KEY
                         | UNIQUE

<ConstraintUnique> ::= UNIQUE
                     |

<ConstraintCluster> ::= CLUSTERED
                      | NONCLUSTERED
                      |

<ConstraintCheck> ::= CHECK <OptionalNotForReplication> '(' <Expression> ')'

<ConstraintIndex> ::= WITH_FILLFACTOR '=' <IntegerLiteral>
                    | <IndexOptionGroup>

! =============================================================================
! Common Table Expressions, TOP and OUTPUT clauses
! =============================================================================

<CTEGroup> ::= WITH <CTEList>
             |

<CTEList> ::= <CTE> ',' <CTEList>
            | <CTE>

<CTE> ::= <AliasName> AS '(' <SelectGroup> ')'

<Top> ::= TOP '(' <Expression> ')' <OptionalPercent>
        |

<OptionalOutputClause> ::= OUTPUT <OutputColumnList> <OutputIntoClause>
                         |

<OutputIntoClause> ::= INTO <TableName> <ColumnNameGroup>
                     | INTO <VariableName> <ColumnNameGroup>
                     |

<OutputColumnList> ::= <OutputColumn> ',' <OutputColumnList>
                     | <OutputColumn>

<OutputColumn> ::= <ColumnWildQualified> <OptionalAs> ! this includes DELETED and INSERTED pseudo-table names
                 | <Expression> <OptionalAs>

! =============================================================================
! INSERT Statement
! =============================================================================

<InsertStatement> ::= <CTEGroup> INSERT <Top> <OptionalInto> <WriteableRowset> <InsertData>

<InsertData> ::= <ColumnNameGroup> <OptionalOutputClause> <InsertSource>
               | DEFAULT VALUES

<OptionalInto> ::= INTO
                 |

<InsertSource> ::= VALUES '(' <ExpressionList> ')'
                 | <SelectGroup>
                 | <ExecuteStatement>


! =============================================================================
! UPDATE Statement
! =============================================================================

<UpdateStatement> ::= <CTEGroup> UPDATE <Top> <WriteableRowset> SET <UpdateColumnList> <OptionalOutputClause> <OptionalFromClause> <WhereClause>

<UpdateColumnList> ::= <UpdateColumn> ',' <UpdateColumnList>
                     | <UpdateColumn>

<UpdateColumn> ::= <ColumnNameQualified> '=' <ExpressionOrDefault>
                 | <VariableName> '=' <Expression>
                 | <VariableName> '=' <ColumnNameQualified> '=' <Expression>

<ExpressionOrDefault> ::= <Expression>
                        | DEFAULT

<OptionalFromClause> ::= <FromClause>
                       |

! =============================================================================
! DELETE Statement
! =============================================================================

<DeleteStatement> ::= <CTEGroup> DELETE <Top> <OptionalFrom> <WriteableRowset> <OptionalOutputClause> <OptionalFromClause> <WhereClause>

<OptionalFrom> ::= FROM
                 |

! =============================================================================
! SELECT Statement
! =============================================================================

<SelectStatement> ::= <CTEGroup> <SelectGroup> <SelectFor>

<SelectGroup> ::= SELECT <TopLegacy> <Columns> <IntoClause> <FromClauseFull> <UnionClause>

<TopLegacy> ::= TOP IntegerLiteral <OptionalPercent>
              | <Top>

<OptionalPercent> ::= PERCENT
                    |

<Columns> ::= <Restriction> <ColumnList>

<ColumnList> ::= <ColumnItem> ',' <ColumnList>
               | <ColumnItem>

<ColumnItem> ::= <ColumnWildQualified>
               | <Expression> <OptionalAlias>

<Restriction> ::= ALL
                | DISTINCT
                |

<IntoClause> ::= INTO <TableName>
               | INTO <VariableName>
               |

<FromClause> ::= FROM <Source> <JoinChain>

<FromClauseFull> ::= <FromClause> <WhereClause> <GroupClause> <HavingClause> <OptionalOrderClause> 
               |

<JoinChain> ::= <Join> <JoinChain>
              |

<Join> ::= <JoinKind> JOIN <Source> ON <Predicate>
         | CROSS JOIN <Source>

<JoinKind> ::= <OptionalInner>
             | LEFT <OptionalOuter>
             | RIGHT <OptionalOuter>
             | FULL <OptionalOuter>

<OptionalInner> ::= INNER
                  |

<OptionalOuter> ::= OUTER
                  |

<Source> ::= <ReadableRowset> <OptionalAlias>

<OptionalAlias> ::= <OptionalAs> <AliasName>
                  |

<WhereClause> ::= WHERE <Predicate>
                |

<GroupClause> ::= GROUP BY <ExpressionList>
                |

<OrderClause> ::= ORDER BY <OrderList>

<OptionalOrderClause> ::= <OrderClause>
                        |

<OrderList> ::= <Order> ',' <OrderList>
              | <Order>

<Order> ::= <Expression> <OrderType>

<OrderType> ::= ASC
              | DESC
              |

<HavingClause> ::= HAVING <Predicate>
                 |

<UnionClause> ::= UNION <SelectGroup>
                | UNION ALL <SelectGroup>
                |

<SelectFor> ::= FOR BROWSE
              | FOR_XML <SelectForList>
              |

<SelectForList> ::= <SelectForIdList>
                  | <SelectForIdList> ',' <SelectForList>

<SelectForIdList> ::= Id <SelectForIdList>
                    | Id '(' <XmlElementName> ')'
                    | Id '(' <StringLiteral> ')'
                    | Id
           
! ============================================================================= 
! Pedicates and Expressions
! =============================================================================

<Predicate> ::= <OrExp>

<OrExp> ::= <AndExp> OR <OrExp>
          | <AndExp>

<AndExp> ::= <NotExp> AND <AndExp>
           | <NotExp>

<NotExp> ::= NOT <PredBetween>
           | <PredBetween>

<PredBetweenOp> ::= BETWEEN
                  | NOT BETWEEN

<PredBetween> ::= <Expression> <PredBetweenOp> <Expression> AND <Expression>
                | <PredNull>

<PredNull> ::= <Expression> IS <NullCheck>
             | <PredLike>

<PredLike> ::= <Expression> <OptionalNot> LIKE <CollableStringLiteral> <Escape>
             | <PredIn>

<OptionalNot> ::= NOT
                |

<PredIn> ::= <Expression> <OptionalNot> IN <Tuple>
           | <PredExists>

<PredExists> ::= EXISTS '(' <SelectGroup> ')'
               | <PredExp>

<PredBinaryOp> ::= '='
                 | '<>'
                 | '>'
                 | '>='
                 | '<'
                 | '<='

<PredExp> ::= <Expression> <PredBinaryOp> <Expression>
            | '(' <OrExp> ')'

<Escape> ::= ESCAPE StringLiteral
           |

<Expression> ::= <AddExp>

<AddExp> ::= <MultExp> <AddBinaryOp> <AddExp>
           | <MultExp>

<AddBinaryOp> ::= '+'
                | '-'

<MultExp> ::= <MultExp> <MultBinaryOp> <NegateExp> 
            | <NegateExp>

<MultBinaryOp> ::= '*'
                 | '/'

<NegateExp> ::= '-' <Function>
              | <Function>

<Function> ::= <CountFunction>
             | <CastFunction>
             | <CoalesceFunction>
             | <FunctionCall>
             | <FunctionCall> OVER '(' <PartitionByClause> ')'
             | <Case>
             
<PartitionByClause> ::= PARTITION_BY <ExpressionList> <OptionalOrderClause>
                      | <OrderClause>

<FunctionCall> ::= <FunctionName> '(' ')'
                 | <FunctionName> '(' <ExpressionList> ')'

<CountFunction> ::= COUNT_ <Restriction> <ColumnWildNameQualified> ')'

<CastFunction> ::= CAST_ <Expression> AS <TypeName> ')'

<CoalesceFunction> ::= COALESCE '(' <ExpressionList> ')'

<Case> ::= CASE <Expression> <CaseWhenExpressionList> END
         | CASE <CaseWhenExpressionList> END
         | <Value>

<CaseWhenExpressionList> ::= <CaseWhenExpression> <CaseWhenExpressionList>
                           | <CaseWhenExpression> <CaseElseExpression>
                           
<CaseWhenExpression> ::= WHEN <Predicate> THEN <Expression>

<CaseElseExpression> ::= ELSE <Expression>
                       |

<Value> ::= <CollableValue> <OptionalCollate>
          | <Literal>
     
<CollableValue> ::= <Tuple>
                  | <SystemVariableName>
                  | <ColumnNameQualified>
                  | <VariableName> ! CLR UDT property, field and methods are not yet implemented

<Tuple> ::= '(' <SelectGroup> ')'
          | '(' <ExpressionList> ')'

<ExpressionList> ::= <Expression> ',' <ExpressionList>
                   | <Expression>

